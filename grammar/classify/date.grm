# Apache 2.0
# Based on en-toy
import '../byte.grm' as b;
import '../util.grm' as u;

d = b.kDigit;
q = u.q;

month_file = StringFile['months.tsv', 'utf8', 'utf8'];

# Allows both abbreviations and full names:
month = month_file | Project[month_file, 'output'];

# 1-31, optional '0'
day_num = ("0" : "")? (d - "0") | "1" d | "2" d | "30" | "31";

# Nós has 2ú, so there's no need to make this more complicated
day_ord = "1d" | "2a" | day_num "ú".utf8 | day_num "adh";

day = day_num | day_ord;

year = ("1" | "2") d{3};

dmy =
  u.I["date { "]
  u.I["day: " q]
  day
  u.D[" "+]
  u.I[q " month: " q]
  month
  u.D[","]?
  u.D[" "+]
  u.I[q " year: " q]
  year
  u.I[q]
  u.I[" preserve_order: true"]
  u.I[" }"];

export DATE = Optimize[dmy];
